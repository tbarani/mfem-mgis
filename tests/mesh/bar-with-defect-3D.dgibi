'OPTION' 'DIME' 3;
'OPTION' 'ELEM' CUB8;

L = 1.;
lg = 0.1;
h = 0.01;

O = (0.  0. h);
A = ((L '/' 2) 0 h);
B = (L 0. h);
E = (0 lg h);
D = ((L '/' 2) lg h);
C = (L. lg h);

d0 = 5e-3 '*' L;
d1 = 1e-3 '*' L;
L1 = (DROIT O A 'DINI' d0 'DFIN' d1) 'ET' 
     (DROIT A B 'DINI' d1 'DFIN' d0);
L2 = (DROIT E D 'DINI' d0 'DFIN' d1) 'ET' 
     (DROIT D C 'DINI' d1 'DFIN' d0);

eps = 1.e-2;
'DEPLACER' A 'PLUS'  (0 eps 0);
'DEPLACER' D 'MOINS' (0 eps 0);

eps = L '*' 1.e-16;
sup = 'REGLER' 1 l1 l2;
beam = 'VOLUME' 1  sup 'TRANS' (0. 0. (-1 '*' h));

'TRACER' beam;

env_beam = 'ENVELOPPE' beam;
x y z = 'COORDONNEE' env_beam;
pts1 = x 'POINT' 'COMPRIS' (-1 '*' eps) eps;
sleft = env_beam 'ELEMENT' 'APPUYE' 'STRICTEMENT' pts1;
pts2 = x 'POINT' 'COMPRIS' (L '-' eps) (L '+' eps);
sright = env_beam 'ELEMENT' 'APPUYE' 'STRICTEMENT' pts2;
pts3 = y 'POINT' 'COMPRIS' (-1 '*' eps) eps;
sfront = env_beam 'ELEMENT' 'APPUYE' 'STRICTEMENT' pts3;
pts4 = y 'POINT' 'COMPRIS' (lg '-' eps) (lg '+' eps);
srear = env_beam 'ELEMENT' 'APPUYE' 'STRICTEMENT' pts4;
pts5 = z 'POINT' 'COMPRIS' (-1 '*' eps) eps;
sdown = env_beam 'ELEMENT' 'APPUYE' 'STRICTEMENT' pts5;

'TRACER' (('COULEUR' 'ROUGE' sup) et sdown);
'TRACER' (('COULEUR' 'ROUGE' sfront) et srear);
'TRACER' (('COULEUR' 'ROUGE' sright) et sleft);

'OPTION' 'SORTIR' 'bar-with-defect-3D.med';
'SORTIR' MED sdown sup srear sfront sleft sright;
'FIN';